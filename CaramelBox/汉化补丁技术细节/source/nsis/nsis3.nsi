; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "爱上姐姐（♂）的少女DVD版完全版 汉化补丁"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PUBLISHER "瑞穗姐姐推广协会/WLGO"
!define PRODUCT_WEB_SITE "http://www.mycompany.com"   ; !!!!!!!
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\otoboku_cn.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

SetCompressor lzma
;SetCompress off    ; !!!!!!!!!!!!!!

; MUI 1.67 compatible ------
!include "MUI.nsh"
!include "FileFunc.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "nsis\icon\inst.ico"
!define MUI_UNICON "nsis\icon\uninst.ico"

;自定义页眉图标
!define MUI_HEADERIMAGE   
!define MUI_HEADERIMAGE_BITMAP "nsis\bmp\header.bmp"
!define MUI_HEADERIMAGE_RIGHT
;自定义左侧欢迎图片
!define MUI_WELCOMEFINISHPAGE_BITMAP "nsis\bmp\welcome.bmp"
!define MUI_COMPONENTSPAGE_CHECKBITMAP "nsis\bmp\check.bmp"
!ifndef RELEASE
;不是relase下不自动关闭
!define MUI_FINISHPAGE_NOAUTOCLOSE
!endif

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!define MUI_LICENSEPAGE_RADIOBUTTONS
!insertmacro MUI_PAGE_LICENSE "nsis\txt\License.txt"
; Components page
!insertmacro MUI_PAGE_COMPONENTS
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\otoboku_cn.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "SimpChinese"

; Reserve files
!insertmacro MUI_RESERVEFILE_INSTALLOPTIONS
ReserveFile "nsis\bmp\splash.bmp"   
ReserveFile "${NSISDIR}\Plugins\system.dll"   
ReserveFile "nsis\utility.dll"
ReserveFile "nsis\CaramelBox_patch.dll"
;ReserveFile "nsis\music\bgm.mp3"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "爱上姐姐（♂）的少女DVD版完全版汉化补丁.exe"
InstallDir "$PROGRAMFILES"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "安装汉化补丁" SEC01
  DetailPrint "正在释放资源..."
  CreateDirectory "$TEMP\working"
  CreateDirectory "$TEMP\working\resource"
  SetOutPath "$TEMP\working\resource"
  File "nsis\resource\*.*"
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "nsis\files\otoboku_cn.exe"
  File "nsis\txt\README.txt"
  CreateDirectory "$SMPROGRAMS\爱上姐姐（♂）的少女DVD版完全版 汉化补丁"
  CreateShortCut "$SMPROGRAMS\爱上姐姐（♂）的少女DVD版完全版 汉化补丁\爱上姐姐（♂）的少女DVD版完全版 汉化补丁.lnk" "$INSTDIR\otoboku_cn.exe"
  CreateShortCut "$DESKTOP\爱上姐姐（♂）的少女DVD版完全版 汉化补丁.lnk" "$INSTDIR\otoboku_cn.exe"

  DetailPrint "正在修补文件..."
  Var /GLOBAL file_handle
  Var /GLOBAL resource_name
  SetOutPath "$TEMP\working"
  SetOverwrite on
  File "nsis\utility.dll"
  File "nsis\CaramelBox_patch.dll"
  FindFirst $file_handle $resource_name $TEMP\working\resource\*.*
  loop:
    StrCmp $resource_name "." retry
    StrCmp $resource_name ".." retry
    StrCmp $resource_name "" done
    System::Call "CaramelBox_patch::CaramelBox_patch(t, t) i('$INSTDIR\system.bin', '$TEMP\working\resource\$resource_name').r2"
  retry:
    FindNext $file_handle $resource_name
  Goto loop
  done:
  FindClose $file_handle
  DetailPrint "正在删除临时文件..."
  Delete $TEMP\working\resource\*.*
  Delete $TEMP\working\*.*
  RMDir $TEMP\working\resource
  RMDir $TEMP\working
SectionEnd

Section "备份文件" SEC02
  DetailPrint "正在备份文件..."
  IfFileExists $INSTDIR\backup\system.bin over
  CreateDirectory "$INSTDIR\backup"
  CopyFiles /SILENT $INSTDIR\system.bin $INSTDIR\backup\system.bin
  over:
SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  CreateShortCut "$SMPROGRAMS\爱上姐姐（♂）的少女DVD版完全版 汉化补丁\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\otoboku_cn.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\otoboku_cn.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd

; Section descriptions
!insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC01} "安装汉化补丁文件"
  !insertmacro MUI_DESCRIPTION_TEXT ${SEC02} "备份日文原版文件"
!insertmacro MUI_FUNCTION_DESCRIPTION_END

Function .onInit   
  InitPluginsDir   
  File "/oname=$PLUGINSDIR\Splash_Splash.bmp" "nsis\bmp\splash.bmp"
  ; 使用闪屏插件显示闪屏   
  advsplash::show -1 600 400 -1 "$PLUGINSDIR\Splash_Splash"
  Pop $0 ; $0 返回 '1' 表示用户提前关闭闪屏, 返回 '0' 表示闪屏正常结束, 返回 '-1' 表示闪屏显示出错   
  ;!insertmacro MUI_LANGDLL_DISPLAY   
  ; C:\Program Files\Caramel-Box\I女(おとめ)はおさま(ボク)に恋してるDVD版
  StrCpy $INSTDIR "C:\Program Files\Caramel-Box\I女(おとめ)はおさま(ボク)に恋してるDVD版"
  File "/oname=$PLUGINSDIR\bgm.mp3" "nsis\music\bgm.mp3"
  ; 打开音乐文件
  System::Call "winmm.dll::mciSendString(t 'OPEN $PLUGINSDIR\bgm.mp3 TYPE MPEGVIDEO ALIAS BGMUSIC', t .r0, i 130, i 0)"
  ; 开始循环播放音乐文件
  System::Call "winmm.dll::mciSendString(t 'PLAY BGMUSIC NOTIFY REPEAT', t .r0, i 130, i 0)"
FunctionEnd   

Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) 已成功地从你的计算机移除。"
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "你确实要完全移除 $(^Name) ，其及所有的组件？" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\otoboku_cn.exe"

  DetailPrint "正在恢复文件..."
  IfFileExists $INSTDIR\backup\system.bin recover skip
  recover:
  CopyFiles /SILENT $INSTDIR\backup\system.bin $INSTDIR\system.bin
  Delete $INSTDIR\backup\system.bin
  skip:
  RMDir "$INSTDIR\backup"
  
  Delete "$SMPROGRAMS\爱上姐姐（♂）的少女DVD版完全版 汉化补丁\Uninstall.lnk"
  Delete "$DESKTOP\爱上姐姐（♂）的少女DVD版完全版 汉化补丁.lnk"
  Delete "$SMPROGRAMS\爱上姐姐（♂）的少女DVD版完全版 汉化补丁\爱上姐姐（♂）的少女DVD版完全版 汉化补丁.lnk"

  RMDir "$SMPROGRAMS\爱上姐姐（♂）的少女DVD版完全版 汉化补丁"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd